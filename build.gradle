plugins {
    id 'java'
    id 'distribution'
    id 'idea'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    maven {
        url "https://dl.bintray.com/omegat-org/maven"
    }
    jcenter()
}

dependencies {
	compileOnly 'org.omegat:omegat:5.4.1'
	compileOnly 'commons-io:commons-io:2.7'
	compileOnly 'commons-lang:commons-lang:2.6'
	compileOnly 'org.omegat:lib-mnemonics:1.0'

	implementation 'commons-cli:commons-cli:1.4'
}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

jar {
    manifest {
        // plugin's main class name is defined in gradle.properties file.
        attributes(
                "OmegaT-Plugins": pluginMainClass,
                "Plugin-Version": project.version,
                "Plugin-Name": pluginName,
                "Plugin-Author": pluginAuthor,
                "Plugin-Description": pluginDescription,
                "Main-Class": pluginMainClass,
                // Used to access the name/version inside the plugin
                "Implementation-Title": pluginName,
                "Implementation-Version": project.version
        )
    }
}

shadowJar {
	// remove the '-all' suffix
	classifier = ''
}

distributions {
    main {
        contents {
            from('README.md', 'COPYING')
            into('docs') {
                from 'CHANGELOG.md'
            }

            from(jar) {
                into 'plugin'
            }
        }
    }
}

task installPlugin(type: Copy) {
        from 'build/install/plugin-omt-package/plugin'
        into omegatPluginDir
        include('*.jar')
}

installPlugin.dependsOn installDist
